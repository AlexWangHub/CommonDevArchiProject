//
// Created by sanhuazhang on 2019/06/03
//

/*
 * Tencent is pleased to support the open source community by making
 * WCDB available.
 *
 * Copyright (C) 2017 THL A29 Limited, a Tencent company.
 * All rights reserved.
 *
 * Licensed under the BSD 3-Clause License (the "License"); you may not use
 * this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 *       https://opensource.org/licenses/BSD-3-Clause
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#import "Core.hpp"
#import "CoreConst.h"
#import "FileHandle.hpp"
#import "Notifier.hpp"
#import "SQLite.h"
#import "WCTConvertible.h"
#import "WCTDatabase+Convenient.h"
#import "WCTDatabase+Private.h"
#import "WCTDatabase+Test.h"
#include <iomanip>

static std::atomic<WCTSimulateIOErrorOptions> &simulateIOErrorOptions()
{
    static std::atomic<WCTSimulateIOErrorOptions> *s_simulateIOErrorOptions = new std::atomic<WCTSimulateIOErrorOptions>(WCTSimulateNoneIOError);
    return *s_simulateIOErrorOptions;
}

static ssize_t controllableWrite(int fd, const void *buf, size_t byte, off_t offset)
{
    if ((simulateIOErrorOptions().load() & WCTSimulateWriteIOError) != 0) {
        return -1;
    }
    return pwrite(fd, buf, byte, offset);
}

static ssize_t controllableRead(int fd, void *buf, size_t byte, off_t offset)
{
    if ((simulateIOErrorOptions().load() & WCTSimulateReadIOError) != 0) {
        return -1;
    }
    return pread(fd, buf, byte, offset);
}

static std::nullptr_t initialize()
{
    sqlite3_vfs *vfs = sqlite3_vfs_find(nullptr);
    vfs->xSetSystemCall(vfs, "pwrite", (sqlite3_syscall_ptr) controllableWrite);
    vfs->xSetSystemCall(vfs, "pread", (sqlite3_syscall_ptr) controllableRead);
    return nullptr;
}

@implementation WCTDatabase (Test)

+ (void)simulateIOError:(WCTSimulateIOErrorOptions)options
{
    WCDB_ONCE(initialize());

    simulateIOErrorOptions().store(options);
}

- (void)enableAutoCheckpoint:(BOOL)flag
{
    WCDB::Core::shared().enableAutoCheckpoint(_database, flag);
}

- (BOOL)truncateCheckpoint
{
    return _database->checkpoint(false, WCDB::InnerDatabase::CheckPointMode::Truncate);
}

- (BOOL)passiveCheckpoint
{
    return _database->checkpoint(false, WCDB::InnerDatabase::CheckPointMode::Passive);
}

- (BOOL)isOpened
{
    return _database->isOpened();
}

- (BOOL)canOpen
{
    return _database->canOpen();
}

- (void)close
{
    _database->close(nullptr);
}

- (BOOL)isBlockaded
{
    return _database->isBlockaded();
}

- (void)blockade
{
    _database->blockade();
}

- (void)unblockade
{
    _database->unblockade();
}

@end
